PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
CONSTRUCT 
{ 



    ### Represent Drug drug interaction. https://biolink.github.io/biolink-model/docs/ChemicalToThingAssociation.html
    # Or should we use https://biolink.github.io/biolink-model/docs/affects.html
    [ a bioentity:ChemicalToThingAssociation ;
    bioentity:relation bioentity:ChemicalToThingAssociation ;
    bioentity:subject ?drugUri ;
    bioentity:object ?drugInteractionUri ;
    bioentity:description ?drugInteractionDescription ] .

    ?drugInteractionUri a bioentity:Drug .
    ?drugInteractionUri bioentity:name ?drugInteractionName .

    ?drugUri bioentity:interacts_with ?foodInteraction .


    }
WHERE {
select * where { 

    # Get drug primary ID
    ?drugObj a x2rm:drugbank\/drug .
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/drugbank-id ; x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ; x2rm:hasValue ?drugId ] .
    BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )

    # Get drug-drug interactions
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/drug-interactions; 
        x2rm:hasChild [ a x2rm:drugbank\/drug\/drug-interactions\/drug-interaction; 
            x2rm:hasChild [ a x2rm:drugbank\/drug\/drug-interactions\/drug-interaction\/drugbank-id; x2rm:hasValue ?drugInteractionId ] ;
            x2rm:hasChild [ a x2rm:drugbank\/drug\/drug-interactions\/drug-interaction\/name; x2rm:hasValue ?drugInteractionName ] ;
            x2rm:hasChild [ a x2rm:drugbank\/drug\/drug-interactions\/drug-interaction\/description; x2rm:hasValue ?drugInteractionDescription ] 
    ] ] .
    BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugInteractionId)) AS ?drugInteractionUri )
    #BIND ( iri(concat("http://data2services/vocab/drugbank_association:", md5(concat(?dbid, ?drugInteractionId)))) AS ?drugInteractionAssociationUri )
    #BIND ( iri(concat("http://data2services/vocab/drugbank_association:", md5(concat(?dbid, ?drugInteractionId)))) AS ?drugInteractionAssociationUri )

    # Get food interactions
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/food-interactions; 
        x2rm:hasChild [ a x2rm:drugbank\/drug\/food-interactions\/food-interaction; x2rm:hasValue ?foodInteraction
    ] ] .

}

}