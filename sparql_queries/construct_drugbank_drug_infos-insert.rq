PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
INSERT 
{ 
  GRAPH <http://data2services/biolink/drugbank>
  {
    # WORKING
    ?drugUri a bioentity:Drug .
    ?drugUri bioentity:name ?drugName .
    ?drugUri bioentity:id ?drugId .
    ?drugUri bioentity:systematic_synonym ?systematicSynonym .
    ?drugUri bioentity:description ?drugDescription .

    # X-ref
    ?drugUri bioentity:same_as ?casNumberUri .
    ?casNumberUri bioentity:id ?casNumber .

    ?drugUri bioentity:same_as ?uniiUri .
    ?uniiUri bioentity:id ?unii .

    # Affected organism
    #?drugUri bioentity:affects ?affectedOrganismUri .
    #?affectedOrganismUri a bioentity:OrganismalEntity .
    #?affectedOrganismUri bioentity:name ?affectedOrganism .

    # TODO: category. Is using this predicate right? https://biolink.github.io/biolink-model/docs/category.html
    ?drugUri bioentity:category ?categoryMeshUri .
    ?categoryMeshUri bioentity:id ?categoryMeshId .
    ?categoryMeshUri bioentity:name ?categoryName .


    # TODO: extract enzymes (structure really similar to targets)

  }
}
WHERE {
GRAPH  <http://data2services/graph/xml2rdf> { 
    # Get drug primary ID
    ?drugObj a x2rm:drugbank\/drug .
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/drugbank-id ; x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ; x2rm:hasValue ?drugId ] .
    BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )


    ### Get Drug single informations (id, name, description)

    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/drugbank-id ; x2rm:hasValue ?systematicSynonym ] .

    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/cas-number; x2rm:hasValue ?casNumber ] .
    BIND ( iri(concat("http://identifiers.org/cas-number:", ?casNumber)) AS ?casNumberUri )

    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/unii; x2rm:hasValue ?unii ] .
    BIND ( iri(concat("http://identifiers.org/unii:", ?unii)) AS ?uniiUri )

    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/name; x2rm:hasValue ?drugName] .
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/description ; x2rm:hasValue ?drugDescription ] .

    # TODO: state (liquid)
    # TODO: synthesis-reference = Patent how it is synthesis
    # TODO: a lot of "text description" have not been done. Trouble to get where they should be defined... https://biolink.github.io/biolink-model/docs/Drug.html
    # indication, pharmacodynamics, mechanism-of-action, toxicity, metabolism, absorption, half-life, protein-binding, route-of-elimination, volume-of-distribution
    # clearance

    # TODO: atc-codes, dosages, patents, sequences


    ### Get CATEGORIES (Mesh)
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/categories;
        x2rm:hasChild [ a x2rm:drugbank\/drug\/categories\/category;  
            x2rm:hasChild [ a x2rm:drugbank\/drug\/categories\/category\/mesh-id; x2rm:hasValue ?categoryMeshId ] ;
            x2rm:hasChild [ a x2rm:drugbank\/drug\/categories\/category\/category; x2rm:hasValue ?categoryName ] 
    ] ] .
    BIND ( iri(concat("http://identifiers.org/mesh:", ?categoryMeshId)) AS ?categoryMeshUri )


    # Get AFFECTED ORGANISM
    #?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/affected-organism; x2rm:hasValue ?affectedOrganism ] .
    #BIND ( iri(concat("http://data2services/vocab/organism/", md5(?affectedOrganism))) AS ?affectedOrganismUri )

}

}