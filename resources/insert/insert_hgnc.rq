PREFIX kraken: <http://kraken//data/ncats/hgnc/hgnc_complete_set.ts/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
PREFIX d2svocab: <http://data2services/vocab/>
INSERT
{
  GRAPH <http://data2services/biolink/hgnc> {  
   ?hgncUri a bioentity:Gene. 
   ?hgncUri bioentity:iri ?hgncUri .
   ?hgncUri bioentity:id ?hgncid .
   ?hgncUri bioentity:name ?name .
   ?hgncUri rdfs:label ?name .
   ?hgncUri bioentity:systematic_synonym ?symbol .

   ?hgncUri d2svocab:Status ?Status .
   # Use bioentity:ConfidenceLevel (Level of confidence in a statement)? Values: "Approved", "Symbol Withdrawn", "Entry Withdrawn"

   ?hgncUri bioentity:category ?LocusType .
   # Could it be bioentity:category? Values: "RNA, long non-coding", "gene with protein product", "pseudogene", "virus integration site"
   ?hgncUri bioentity:category ?LocusGroup .
   # Almost same as LocusType. Values: "non-coding RNA", "protein-coding gene", "pseudogene", "other"

   #?hgncUri bioentity:GenomicSequenceLocalization ?
   # Should we use https://biolink.github.io/biolink-model/docs/located_in.html ?

   ?hgncUri bioentity:update_date ?DateModified .


   # https://www.ncbi.nlm.nih.gov/gene/1047
   ?hgncUri bioentity:same_as ?EntrezGeneUri .
   ?hgncUri bioentity:same_as ?EntrezGeneIdSuppliedByNcbiUri .

   # http://identifiers.org/ensembl/ENSG00000153132
   ?hgncUri bioentity:same_as ?EnsemblGeneUri .

   # http://identifiers.org/mim/601858
   ?hgncUri bioentity:same_as ?OmimIdSuppliedByNcbiUri .

   # https://www.ncbi.nlm.nih.gov/nuccore/D86322
   ?hgncUri bioentity:same_as ?AccessionNumbersUri .

   # https://biolink.github.io/biolink-model/docs/TranscriptToGeneRelationship.html
   ?refseqAssociationUri a bioentity:TranscriptToGeneRelationship ;
            bioentity:transcript_to_gene_relationship_subject ?RefseqIdsUri ;
            bioentity:transcript_to_gene_relationship_object ?hgncUri ;
            bioentity:relation bioentity:expresses .

   ?RefseqIdsUri a bioentity:Transcript . # https://www.ncbi.nlm.nih.gov/nuccore/NM_004362

   ?suppliedRefseqAssociationUri a bioentity:TranscriptToGeneRelationship ;
            bioentity:transcript_to_gene_relationship_subject ?RefseqSuppliedByNcbiUri ;
            bioentity:transcript_to_gene_relationship_object ?hgncUri ;
            bioentity:relation bioentity:expresses .

   ?RefseqSuppliedByNcbiUri a bioentity:Transcript . # https://www.ncbi.nlm.nih.gov/nuccore/NM_001130675


   # https://biolink.github.io/biolink-model/docs/has_gene_product.html
   ?hgncUri bioentity:has_gene_product ?UniprotIdSuppliedByUniprotUri .

   ?hgncUri bioentity:same_as ?EnsemblIdSuppliedByEnsemblUri .

   # https://biolink.github.io/biolink-model/docs/GeneToGeneHomologyAssociation.html
   ?mgiAssociationUri a bioentity:GeneToGeneHomologyAssociation ;
            bioentity:relation bioentity:homologous_to ;
            bioentity:gene_to_gene_association_subject ?hgncUri ;
            bioentity:gene_to_gene_association_object ?MouseGenomeDatabaseIdUri .

   ?suppliedMgiAssociationUri a bioentity:GeneToGeneHomologyAssociation ;
            bioentity:relation bioentity:homologous_to ;
            bioentity:gene_to_gene_association_subject ?hgncUri ;
            bioentity:gene_to_gene_association_object ?MouseGenomeDatabaseIdSuppliedByMgiUri .

   ?rgdAssociationUri a bioentity:GeneToGeneHomologyAssociation ;
            bioentity:relation bioentity:homologous_to ;
            bioentity:gene_to_gene_association_subject ?hgncUri ;
            bioentity:gene_to_gene_association_object ?RatGenomeDatabaseIdSuppliedByRgdUri .



   ?hgncUri bioentity:located_in ?GenomicSequenceLocalizationUri .
   ?hgncUri bioentity:located_in ?CcdsIdsUri .

   ?GenomicSequenceLocalizationUri a bioentity:GenomicSequenceLocalization ;
      bioentity:genomic_sequence_localization_subject ?hgncUri ;
      bioentity:genomic_sequence_localization_object ?locationUri .

    ?locationUri a bioentity:GenomicEntity ;
      bioentity:id ?location ;
      bioentity:name ?location ;
      bioentity:category "chromosome" .



   # Publication
   ?hgncUri bioentity:publications ?PubmedIdsUri .
   ?PubmedIdsUri a bioentity:Publication ;
      bioentity:id ?PubmedIds ;
      bioentity:iri ?PubmedIdsUri ;
      bioentity:relates_to ?hgncUri .

    ?hgncUri bioentity:filler ?GeneFamilyUri .
    ?GeneFamilyUri a <http://data2services/model/GeneFamily> ;
      bioentity:subClassOf bioentity:Attribute ;
      bioentity:description ?GeneFamilyDescription ;
      bioentity:id ?GeneFamilyTag .

    # Use bioentity:ConfidenceLevel? (Level of confidence in a statement) Values: "Approved", "Symbol Withdrawn", "Entry Withdrawn"
    ?hgncUri bioentity:filler ?StatusUri .
    ?StatusUri a <http://data2services/model/Status> ;
          rdfs:subClassOf bioentity:Attribute ;
          bioentity:name ?Status .


 }
}
WHERE {
  SERVICE <http://localhost:7200/repositories/test>  {
    GRAPH <http://data2services/graph/autor2rml> {
      ?s kraken:HgncId ?hgncid .
       OPTIONAL { 
         ?s kraken:name ?name .
       
       } OPTIONAL {
         ?s kraken:symbol ?symbol .
       
       } OPTIONAL { 
         ?s kraken:Status ?Status .
         BIND ( iri(concat("http://data2services/model/status/", REPLACE(?Status, " ", "", "i")) ) AS ?StatusUri )
       
       } OPTIONAL { 
         ?s kraken:LocusType ?LocusType .
       
       } OPTIONAL { 
         ?s kraken:LocusGroup ?LocusGroup .
       
       } OPTIONAL {
         ?s kraken:PreviousSymbols ?PreviousSymbols .
       
       } OPTIONAL {
         ?s kraken:PreviousNames ?PreviousNames .
       
       } OPTIONAL {
         ?s kraken:Synonyms ?Synonyms .
       
       } OPTIONAL {
         ?s kraken:NameSynonyms ?NameSynonyms .
       
       } OPTIONAL {
         ?s kraken:location ?location .
         BIND( if( contains(?location, " "),
              iri("") ,
              iri(concat("http://data2services/model/sequence-localization/", md5(concat(?hgncid, ?location))))
              ) 
         AS ?GenomicSequenceLocalizationUri )

         BIND( if( contains(?location, " "),
              iri("") ,
              iri(concat("http://data2services/model/chromosome/", ?location) )
              ) 
         AS ?locationUri )

         #FILTER NOT EXISTS { contains(?location, " ")  }

         #BIND( iri(concat("http://data2services/model/sequence-localization/", md5(concat(?hgncId, ?location)))) AS ?GenomicSequenceLocalizationUri )

         #BIND( iri(concat("http://data2services/model/chromosome/", ?location) ) AS ?locationUri )
       
       } OPTIONAL {
         ?s kraken:DateApproved ?DateApproved .
       
       } OPTIONAL {
         ?s kraken:DateModified ?DateModified .
       
       } OPTIONAL {
         ?s kraken:DateSymbolChanged ?DateSymbolChanged .
       
       } OPTIONAL {
         ?s kraken:DateNameChanged ?DateNameChanged .
       
       } OPTIONAL {
         ?s kraken:AccessionNumbers ?AccessionNumbers .
         BIND( if( contains(?AccessionNumbers, ","),
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", strbefore(?AccessionNumbers, ",") ) ) ,
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", ?AccessionNumbers ) ) ) 
         AS ?AccessionNumbersUri )
       
       } OPTIONAL {
         ?s kraken:EnzymeIds ?EnzymeIds .
         BIND( if( contains(?EnzymeIds, ","),
              iri(concat("http://identifiers.org/ec-code/", strbefore(?EnzymeIds, ",") ) ) ,
              iri(concat("http://identifiers.org/ec-code/", ?EnzymeIds ) ) ) 
         AS ?EnzymeIdsUri )
       
       } OPTIONAL {
         ?s kraken:EntrezGeneId ?EntrezGeneId .
         BIND( iri(concat("https://www.ncbi.nlm.nih.gov/gene/", ?EntrezGeneId) ) AS ?EntrezGeneIdUri )
       
       } OPTIONAL {
         ?s kraken:EnsemblGeneId ?EnsemblGeneId .
         BIND( iri(concat("http://identifiers.org/ensembl/", ?EnsemblGeneId) ) AS ?EnsemblGeneIdUri )
       
       } OPTIONAL {
         ?s kraken:MouseGenomeDatabaseId ?MouseGenomeDatabaseId .
         BIND( if( contains(?MouseGenomeDatabaseId, ","),
              iri(concat("http://identifiers.org/mgi/", strbefore(?MouseGenomeDatabaseId, ",") ) ) ,
              iri(concat("http://identifiers.org/mgi/", ?MouseGenomeDatabaseId ) ) ) 
         AS ?MouseGenomeDatabaseIdUri )
         BIND ( iri(concat("http://data2services/model/association/homology/", md5(concat(?hgncid, ?MouseGenomeDatabaseId)))) AS ?mgiAssociationUri )

       } OPTIONAL {
         ?s kraken:SpecialistDatabaseLinks ?SpecialistDatabaseLinks .
       
       } OPTIONAL {
         ?s kraken:SpecialistDatabaseIds ?SpecialistDatabaseIds .
       
       } OPTIONAL {
         ?s kraken:PubmedIds ?PubmedIds .
         BIND( if( contains(?PubmedIds, ","),
              iri(concat("http://identifiers.org/pubmed/", strbefore(?PubmedIds, ",") ) ) ,
              iri(concat("http://identifiers.org/pubmed/", ?PubmedIds ) ) ) 
         AS ?PubmedIdsUri )
       
       } OPTIONAL {
         ?s kraken:RefseqIds ?RefseqIds .
         BIND( if( contains(?RefseqIds, ","),
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", strbefore(?RefseqIds, ",") ) ) ,
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", ?RefseqIds ) ) ) 
         AS ?RefseqIdsUri )
         BIND ( iri(concat("http://data2services/model/association/transcript/", md5(concat(?hgncid, ?RefseqIds)))) AS ?refseqAssociationUri )
       
       } OPTIONAL {
         ?s kraken:GeneFamilyTag ?GeneFamilyTag .
         BIND( if( contains(?GeneFamilyTag, ","),
              iri(concat("https://data2services/model/gene-family/", strbefore(?GeneFamilyTag, ",") ) ) ,
              iri(concat("https://data2services/model/gene-family/", ?GeneFamilyTag ) ) ) 
         AS ?GeneFamilyUri )
       
       } OPTIONAL {
         ?s kraken:GeneFamilyDescription ?GeneFamilyDescription .
       
       } OPTIONAL {
         ?s kraken:RecordType ?RecordType .
       
       } OPTIONAL {
         ?s kraken:PrimaryIds ?PrimaryIds .
       
       } OPTIONAL {
         ?s kraken:SecondaryIds ?SecondaryIds .
       
       } OPTIONAL {
         ?s kraken:CcdsIds ?CcdsIds .
         BIND( if( contains(?CcdsIds, ","),
              iri(concat("http://identifiers.org/ccds/", strbefore(?CcdsIds, ",") ) ) ,
              iri(concat("http://identifiers.org/ccds/", ?CcdsIds ) ) ) 
         AS ?CcdsIdsUri )
       
       } OPTIONAL {
         ?s kraken:VegaIds ?VegaIds .
       
       } OPTIONAL {
         ?s kraken:LocusSpecificDatabases ?LocusSpecificDatabases .
       
       } OPTIONAL {
         ?s kraken:EntrezGeneIdSuppliedByNcbi ?EntrezGeneIdSuppliedByNcbi .
         BIND( iri(concat("https://www.ncbi.nlm.nih.gov/gene/", ?EntrezGeneIdSuppliedByNcbi ) ) AS ?EntrezGeneIdSuppliedByNcbiUri )
       
       } OPTIONAL {
         ?s kraken:OmimIdSuppliedByNcbi ?OmimIdSuppliedByNcbi .
         # TODO: also take the STRAFTER?
         BIND( if( contains(?OmimIdSuppliedByNcbi, ","),
              iri(concat("http://identifiers.org/mim/", strbefore(?OmimIdSuppliedByNcbi, ",") ) ) ,
              iri(concat("http://identifiers.org/mim/", ?OmimIdSuppliedByNcbi ) ) ) 
         AS ?OmimIdSuppliedByNcbiUri )
                
       } OPTIONAL {
         ?s kraken:RefseqSuppliedByNcbi ?RefseqSuppliedByNcbi .
         BIND( iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", ?RefseqSuppliedByNcbi ) ) AS ?RefseqSuppliedByNcbiUri )
         BIND( iri(concat("http://data2services/model/association/transcript/", md5(concat(?hgncid, ?RefseqSuppliedByNcbi)))) AS ?suppliedRefseqAssociationUri )
       
       } OPTIONAL {
         ?s kraken:UniprotIdSuppliedByUniprot ?UniprotIdSuppliedByUniprot .
         # If there is a comma we take only the first iteration.
         # TODO: also take the STRAFTER?
         BIND( if( contains(?UniprotIdSuppliedByUniprot, ","),
              iri(concat("http://identifiers.org/uniprot/", strbefore(?UniprotIdSuppliedByUniprot, ",") ) ) ,
              iri(concat("http://identifiers.org/uniprot/", ?UniprotIdSuppliedByUniprot ) ) ) 
         AS ?UniprotIdSuppliedByUniprotUri )

       } OPTIONAL {
         ?s kraken:EnsemblIdSuppliedByEnsembl ?EnsemblIdSuppliedByEnsembl .
         BIND( iri(concat("http://identifiers.org/ensembl/", ?EnsemblIdSuppliedByEnsembl) ) AS ?EnsemblIdSuppliedByEnsemblUri )

         # "name": "stderr",
         # "text": [
         # "http://identifiers.org/ensembl/<!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <a href=\"http:/cancer.sanger.ac.uk/cosmic/gene/overview?ln=FBXO28\">COSMIC</a><!--,--> <a href=\"http://www.orpha.net/consor/cgi-bin/OC_Exp.php?Lng=GB&Expert=470322\">Orphanet</a><!--,--> <!--,--> <!--,--> <!--,--> <!--,--> <!--,-->  does not look like a valid URI, trying to serialize this will break.\n"

       
       } OPTIONAL {
         ?s kraken:VegaIdSuppliedByVega ?VegaIdSuppliedByVega .
       
       } OPTIONAL {
         ?s kraken:UcscIdSuppliedByUcsc ?UcscIdSuppliedByUcsc .
       
       } OPTIONAL {
         ?s kraken:MouseGenomeDatabaseIdSuppliedByMgi ?MouseGenomeDatabaseIdSuppliedByMgi .
         BIND( if( contains(?MouseGenomeDatabaseIdSuppliedByMgi, ","),
              iri(concat("http://identifiers.org/mgi/", strbefore(?MouseGenomeDatabaseIdSuppliedByMgi, ",") ) ) ,
              iri(concat("http://identifiers.org/mgi/", ?MouseGenomeDatabaseIdSuppliedByMgi ) ) ) 
         AS ?MouseGenomeDatabaseIdSuppliedByMgiUri )
         BIND ( iri(concat("http://data2services/model/association/homology/", md5(concat(?hgncid, ?MouseGenomeDatabaseIdSuppliedByMgi)))) AS ?suppliedMgiAssociationUri )

       } OPTIONAL {
         ?s kraken:RatGenomeDatabaseIdSuppliedByRgd ?RatGenomeDatabaseIdSuppliedByRgd .
         BIND( if( contains(?RatGenomeDatabaseIdSuppliedByRgd, ","),
              iri(concat("http://identifiers.org/mgi/", strbefore(?RatGenomeDatabaseIdSuppliedByRgd, ",") ) ) ,
              iri(concat("http://identifiers.org/mgi/", ?RatGenomeDatabaseIdSuppliedByRgd ) ) ) 
         AS ?RatGenomeDatabaseIdSuppliedByRgdUri )
         BIND ( iri(concat("http://data2services/model/association/homology/", md5(concat(?hgncid, ?RatGenomeDatabaseIdSuppliedByRgd)))) AS ?rgdAssociationUri )
       
       } OPTIONAL {
         ?s kraken:RnacentralIdsSuppliedByRnacentral ?RnacentralIdsSuppliedByRnacentral .
         BIND( iri(concat("https://rnacentral.org/rna/", ?RnacentralIdsSuppliedByRnacentral) ) AS ?RnacentralIdsSuppliedByRnacentralUri )
       
       } OPTIONAL {
         ?s kraken:LncipediaSymbolSuppliedByLncipedia ?LncipediaSymbolSuppliedByLncipedia .
       }

  BIND ( iri(concat("http://identifiers.org/", lcase(?hgncid))) AS ?hgncUri )

  # Avoid taking the first row with columns labels
  FILTER( !contains(?hgncid, " ") )
}
}
}
