PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
INSERT { 
    GRAPH <http://data2services/biolink/drugbank> {   

        ?carrierUri a bioentity:GeneOrGeneProduct ;
            bioentity:same_as ?polypeptideAttrUri ;
            bioentity:in_taxon ?ncbiTaxonIdentifierUri ;
            bioentity:category ?polypeptidePfamUri .

        # Get GO TERM
        ?goTermAssociationUri a bioentity:GeneToGoTermAssociation ;
            bioentity:relation bioentity:GeneToGoTermAssociation ;
            bioentity:gene_to_go_term_association_object ?carrierUri ;
            bioentity:gene_to_go_term_association_subject ?goTermUri .

        ?goTermUri a bioentity:GeneOntologyClass ;
            bioentity:description ?polyGoTermDescription ;
            bioentity:category ?goTermCategoryUri .

        ?goTermCategoryUri a bioentity:OntologyClass ;
            bioentity:name ?polyGoTermCategory .


        # Get Pfam
        ?polypeptidePfamUri bioentity:id ?polyPfamId ;
            bioentity:name ?polyPfamName . 

        # Get TAXON
        ?ncbiTaxonIdentifierUri a bioentity:OrganismTaxon ;
            bioentity:id ?ncbiTaxonId ;
            bioentity:name ?ncbiTaxonName ;
            bioentity:same_as ?ncbiTaxonUri .

        #?polypeptideAttrUri a bioentity:GeneOrGeneProduct ;
        #    bioentity:same_as ?polypeptideAttrUniprotUri .

    }
}
WHERE {
    SERVICE <http://localhost:7200/repositories/test>  {
        GRAPH <http://kraken/graph/xml2rdf> {

            # Iterate directly on carriers
            ?carrierObj a x2rm:drugbank\/drug\/carriers\/carrier ;
                # Get carrier ID
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/carriers\/carrier\/id ; 
                    x2rm:hasValue ?carrierId
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide ;
                    x2rm:hasAttribute [
                        a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/\@id ;
                        x2rm:hasValue ?polypeptideAttrId
                    ] ;
                    x2rm:hasAttribute [
                        a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/\@source ;
                        x2rm:hasValue "Swiss-Prot"
                    ] ;
                    x2rm:hasChild [ 
                        a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/pfams ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/pfams\/pfam ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/pfams\/pfam\/identifier ;
                                x2rm:hasValue ?polyPfamId
                            ] ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/pfams\/pfam\/name ;
                                x2rm:hasValue ?polyPfamName
                            ]
                        ]
                    ] ;
                    x2rm:hasChild [ 
                        a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/go-classifiers ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/go-classifiers\/go-classifier ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/go-classifiers\/go-classifier\/category ;
                                x2rm:hasValue ?polyGoTermCategory
                            ] ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/go-classifiers\/go-classifier\/description ;
                                x2rm:hasValue ?polyGoTermDescription
                            ]
                        ]
                    ] ;
                    x2rm:hasChild [ 
                        a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/organism ;
                        x2rm:hasAttribute [ 
                            a x2rm:drugbank\/drug\/carriers\/carrier\/polypeptide\/organism\/\@ncbi-taxonomy-id ;
                            x2rm:hasValue ?ncbiTaxonId
                        ] ;
                        x2rm:hasValue ?ncbiTaxonName
                    ]
                ]

            BIND ( iri(concat("http://identifiers.org/drugbank/", ?carrierId)) AS ?carrierUri )
            BIND ( iri(concat("http://identifiers.org/uniprot/", ?polypeptideAttrId)) AS ?polypeptideAttrUri )
            #BIND ( iri(concat("https://purl.uniprot.org/uniprot/", ?polypeptideAttrId)) AS ?polypeptideAttrUniprotUri )

            BIND ( iri(concat("http://data2services/model/association/go-term/", md5(concat(?carrierId, ?polyGoTermDescription)))) AS ?goTermAssociationUri )
            BIND ( iri(concat("http://data2services/model/go-classifier/", md5(?polyGoTermDescription))) AS ?goTermUri )
            BIND ( iri(concat("http://data2services/model/go-category/", ?polyGoTermCategory)) AS ?goTermCategoryUri )
            BIND ( iri(concat("http://identifiers.org/pfam/", ?polyPfamId)) AS ?polypeptidePfamUri )

            BIND ( iri(concat("http://identifiers.org/taxonomy/", ?ncbiTaxonId)) AS ?ncbiTaxonIdentifierUri )
            BIND ( iri(concat("https://www.ncbi.nlm.nih.gov/taxonomy/", ?ncbiTaxonId)) AS ?ncbiTaxonUri )

        }
    }
}