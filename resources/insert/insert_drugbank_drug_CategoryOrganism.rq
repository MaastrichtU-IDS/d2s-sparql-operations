PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
INSERT { 
    GRAPH <http://data2services/biolink/drugbank/test>
    { 
        # Affected organism
        ?drugUri bioentity:affects ?affectedOrganismUri .
        ?affectedOrganismUri a bioentity:OrganismalEntity .
        ?affectedOrganismUri bioentity:name ?affectedOrganism .

        # TODO: category. Is using this predicate right? https://biolink.github.io/biolink-model/docs/category.html
        ?drugUri bioentity:category ?categoryMeshUri .
        ?categoryMeshUri bioentity:id ?categoryMeshId .
        ?categoryMeshUri bioentity:name ?categoryName .
    }
}
WHERE {
    GRAPH  <http://kraken/graph/xml2rdf> { 
        
        ### Get CATEGORIES (Mesh)
        ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/categories;
            x2rm:hasChild [ a x2rm:drugbank\/drug\/categories\/category;  
                x2rm:hasChild [ a x2rm:drugbank\/drug\/categories\/category\/mesh-id; x2rm:hasValue ?categoryMeshId ] ;
                x2rm:hasChild [ a x2rm:drugbank\/drug\/categories\/category\/category; x2rm:hasValue ?categoryName ] 
        ] ] .
        BIND ( iri(concat("http://identifiers.org/mesh:", ?categoryMeshId)) AS ?categoryMeshUri )


        # Get AFFECTED ORGANISM
        ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/affected-organisms; 
            x2rm:hasChild [ a x2rm:drugbank\/drug\/affected-organisms\/affected-organism;  x2rm:hasValue ?affectedOrganism ] ] .
        BIND ( iri(concat("http://data2services/vocab/organism/", md5(?affectedOrganism))) AS ?affectedOrganismUri )

    }

}