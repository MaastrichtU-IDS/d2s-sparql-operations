PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
CONSTRUCT 
{ 
    # Pathways (use blank node)
    [ a bioentity:ChemicalToPathwayAssociation ;
     bioentity:relation bioentity:ChemicalToPathwayAssociation ;
     bioentity:subject ?drugUri ;
     bioentity:object ?drugPathwayUri ] .

    ?drugPathwayUri a bioentity:Pathway .
    ?drugPathwayUri bioentity:id ?drugPathwaySmpdbId .
    ?drugPathwayUri bioentity:name ?drugPathwayName .

    }
WHERE {
select * where { 
    # Get drug primary ID
    ?drugObj a x2rm:drugbank\/drug .
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/drugbank-id ; x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ; x2rm:hasValue ?drugId ] .
    BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )


    # Get PATHWAYS
    ?drugObj x2rm:hasChild [ a x2rm:drugbank\/drug\/pathways; 
        x2rm:hasChild ?pathwayObj ] .
    ?pathwayObj a x2rm:drugbank\/drug\/pathways\/pathway;
            x2rm:hasChild [ a x2rm:drugbank\/drug\/pathways\/pathway\/smpdb-id; x2rm:hasValue ?drugPathwaySmpdbId ] ;
            x2rm:hasChild [ a x2rm:drugbank\/drug\/pathways\/pathway\/name; x2rm:hasValue ?drugPathwayName ] .

     BIND ( iri(concat("http://identifiers.org/smpdb:", ?drugPathwaySmpdbId) ) AS ?drugPathwayUri )
     
     # Use bnode instead: BIND ( iri(concat("http://data2services/vocab/pathway_association:", md5(concat(?drugId, ?drugPathwaySmpdbId)))) AS ?pathwayAssociationUri )

}

}